* Make water box input
* 

DIMENS CHSIZE 2000000

! Set variables
! -------------
set in     = prot_xwat              ! protein name
set struc  = @{in}                ! name of input files
set out    = @{in}_waterbox       ! name of output files
set cushion = 12

! Parameters
! ----------
! read in topologies and parameters
set TOPPAR = /home/craigp/software/c36b2/toppar
set top = @TOPPAR/top_all22_prot.rtf
set par = @TOPPAR/par_all22_prot.prm
set watstr = @TOPPAR/toppar_water_ions.str
set phstr = @TOPPAR/toppar_phmd_c22_foramber.str
set segid = PROT

set cpdphmidinp = @TOPPAR/ephmd_pme_coion.in

open unit 1 read form name @top
read rtf card unit 1
close unit 1

open unit 1 read form name @par
read param card unit 1 flex
close unit 1

stream @phstr
stream @watstr

set cubox = @toppar/tip216.crd

! Read PSF and Coordinates
! ------------------------
open read unit 10 card name @struc.psf
read psf  unit 10 card

open read unit 10 card name @struc.crd
read coor unit 10 card

! Determine the molecular extent
! ------------------------------
coor orient
coor stat sele all end

calc Xinit = int ( ( ?Xmax - ?Xmin ) + 2 * @cushion ) + 1
calc Yinit = int ( ( ?Ymax - ?Ymin ) + 2 * @cushion ) + 1
calc Zinit = int ( ( ?Zmax - ?Zmin ) + 2 * @cushion ) + 1
calc Lbox = @Xinit
if Yinit .gt. @Lbox calc Lbox = @Yinit
if Zinit .gt. @Lbox calc Lbox = @Zinit

calc Xinit = @Lbox
calc Yinit = @Lbox
calc Zinit = @Lbox
calc Rinit = @Lbox / 2.0

calc xcen = 0.0
calc ycen = 0.0
calc zcen = 0.0

delete atom sele all end

! Parameters for water box & image (crystal) 
! ------------------------------------------
set XTLtype  = CUBIC
set A = @Xinit 
set B = @A
set C = @A
set Alpha = 90
set Beta  = 90
set Gamma = 90

! Parameters for water box & image (crystal) 
! ------------------------------------------
!set XTLtype  = OCTAHEDRAL
!set A = @Xinit 
!set B = @A
!set C = @A
!set Alpha = 109.471220634
!set Beta  = 109.471220634
!set Gamma = 109.471220634

calc BoxSizeX = @A * 1.1
calc BoxSizeY = @A * 1.1
calc BoxSizeZ = @A * 1.1

! A pre-equilibrated water cubic box with L=18.8560
! This is from tip216.crd
! -------------------------------------------------
set L 18.8560


! number of boxes along XYZ-directions
! ------------------------------------
calc Xnum = int(@BoxSizeX/@L) + 1
calc Ynum = int(@BoxSizeY/@L) + 1
calc Znum = int(@BoxSizeZ/@L) + 1

! base unit of water box
! ----------------------
read sequence TIP3 216
generate W000 setup noangle nodihedral

open read unit 10 card name @cubox
read coor unit 10 card 
close unit 10

coor stat sele type OH2 end
calc Lhalf = @L / 2.0
coor trans xdir 1.0 dist @Lhalf
coor trans ydir 1.0 dist @Lhalf
coor trans zdir 1.0 dist @Lhalf
coor stat sele type OH2 end

! planar water box unit (XY)
! --------------------------
set J2  1
label DO_2
    set J1  1
    label DO_1

    calc wsegid = ( @J2 - 1 ) * @Xnum + @J1

        read sequence TIP3 216
        generate W@wsegid setup noangle nodihedral

    coor duplicate select segid W000 end select segid W@wsegid end

    calc X = @L * ( @J1 - 1 )  ! mult X by @J1
    calc Y = @L * ( @J2 - 1 )  ! mult Y by @J2

    coor trans xdir @X ydir @Y select segid W@wsegid end

    incr J1 by 1
    if J1 le @Xnum goto DO_1
incr J2 by 1
if J2 le @Ynum goto DO_2

delete atom sele .byres. ( ( type OH2 ) .and. -
                           ( prop X .gt. @BoxSizeX .or. -
                             prop Y .gt. @BoxSizeY ) ) end

define solv sele .byres. type OH2 end
if ?nsel .eq. 0 stop ! ABNORMAL TERMINATION: Too small box size 

delete atom sele segid W000 end

open write unit 10 card name water_tmp.crd
write coor unit 10 card

delete atom sele all end

! generate water box by stacking planar water boxes along Z
! ---------------------------------------------------------
set J3  1
label DO_3

    open read card unit 10 name water_tmp.crd
    read sequence coor card unit 10
    generate Wz@J3 setup warn noangle nodihedral 

    open read unit 10 card name water_tmp.crd
    read coor unit 10 card append

    calc Z = @L * ( @J3 - 1 )  ! mult Y by @J3
    coor trans zdir @Z select segid Wz@J3 end

incr J3 by 1
if J3 le @Znum goto DO_3

delete atom sele .byres. ( ( type OH2 ) .and. -
                           ( prop Z .gt. @BoxSizeZ ) ) end
                           
coor stat sele type OH2 end
coor orient norotation 
coor stat sele type OH2 end

! Shaping the box
! ---------------

COOR CONVERT ALIGNED SYMMETRIC @A @B @C @alpha @beta @gamma
coor copy comp

CRYSTAL DEFINE @XTLtype @A @B @C @alpha @beta @gamma
crystal build noper 0 cutoff 2.0                    ! Build crystal
open write card unit 1 name crystal_image.str       !  Build images
crystal write card unit 1


! Image centering by residue
! --------------------------
IMAGE BYRESID XCEN @xcen YCEN @ycen ZCEN @zcen
!nbond ctonnb 2.0 ctofnb 3.0 cutnb 3.0 cutim 3.0 wmin 0.001
nbond ctonnb 10.0 ctofnb 12.0 cutnb 16.0 cutim 14.0 wmin 1.0
CRYSTAL FREE
coor diff comp
delete atom sele .byres. ( ( prop Xcomp .ne. 0 ) .or. -
                           ( prop Ycomp .ne. 0 ) .or. -
                           ( prop Zcomp .ne. 0 ) ) end

COOR CONVERT SYMMETRIC ALIGNED @A @B @C @alpha @beta @gamma

coor stat sele type OH2 end
set nwater ?nsel

! Write output
! ------------
open write unit 10 card name @{out}.crd
write coor unit 10 card
* Equilibrated water 
*

open write card unit 2 name @{out}.pdb
write coor pdb  unit 2 
* Equilibrated water 
*

open write unit 90 card name @{out}.str
write title unit 90
* read sequence TIP3 @nwater
* generate SOLV setup noangle nodihedral
*

open  write unit 90 card name @{out}.prm
write title unit 90
* set BoxType  = OCTAHEDRAL
* set XTLtype  = @XTLtype
* set A = @A
* set B = @B
* set C = @C
* set Alpha = @Alpha
* set Beta  = @Beta
* set Gamma = @Gamma
* set xcen = @xcen
* set ycen = @ycen
* set zcen = @zcen
*

stop
